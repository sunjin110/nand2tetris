class String {

    field Array chars;
    field int max;
    field int length;

    // new 新しいStringを生成する
    constructor String new(int maxLength) {
        let chars = Array.new(maxLength);
        let max = maxLength;
        let length = 0;
        return this;
    }

    method void dispose() {
        do chars.dispose();
        return;
    }

    method int length() {
        return length;
    }

    method char charAt(int i) {
        return chars[i];
    }
    
    method void setCharAt(int i, char c) {
        let chars[i] = c;
        return;
    }

    method String appendChar(char c) {
        if (length < max) {
            let chars[length] = c;
            let length = length + 1;
        }
        return this;
    }

    method void eraseLastChar() {
        // 長さを-1して事実上最後の文字列をcutする
        if (length > 0 ) {
            let length = length - 1;
        }
        return;
    }

    method int intValue() {

        var int intVal;
        var int i;
        var boolean neg;

        let intVal = 0;

        // -かどうか
        if ( (length > 0) & (chars[0] = 45) ) {
            let neg = true;
            let i = 1;
        } else {
            let neg = false;
            let i = 0;
        }

        while( (i < length) & String.isDigit(chars[i]) ) {
            let intVal = (intVal * 10) + String.digitVal(chars[i]);
            let i = i + 1;
        }

        if (neg) {
            return -intVal;
        } else {
            return intVal;
        }
    }

    method void setInt(int num) {

        let length = 0; // char str

        if (num < 0) {
            let num = -num;
            do appendChar(45); // -
        }

        do doSetInt(num);
        return;
    }

    function char backSpace() {
        return 129;
    }

    function char doubleQuote() {
        return 34;
    }

    function char newLine() {
        return 128;
    }


    function boolean isDigit(char c) {
        return ~(c < 48) & ~(c > 57);
    }

    function int digitVal(char c) {
        return c - 48;
    }

    function char digitChar(int i) {
        return i + 48;
    }

    method void doSetInt(int num) {
        var int q;
        var int mod;
        var char c;

        let q = num / 10;
        let mod = num - (q * 10);
        let c = String.digitChar(mod);

        if (num < 10) {
            do appendChar(c);
        } else {
            do doSetInt(q);
            do appendChar(c);
        }
        return;
    }




}
